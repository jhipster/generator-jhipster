ext {
    javaLanguageLevel = 1.<%= javaVersion %>
    generatedMapperSourcesDir = "${buildDir}/generated-src"
}

configurations {
    mapstruct
}

dependencies {
    compile group: 'org.mapstruct', name: 'mapstruct', version: mapstruct_version
    mapstruct group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstruct_version
}

sourceSets.main {
    ext.originalJavaSrcDirs = java.srcDirs
    java.srcDir "${generatedMapperSourcesDir}"
}

task generateMainMapperClasses(type: JavaCompile) {
    ext.aptDumpDir = file( "${buildDir}/tmp/apt/mapstruct" )
    destinationDir = aptDumpDir

    classpath = compileJava.classpath + configurations.mapstruct
    source = sourceSets.main.originalJavaSrcDirs
    ext.sourceDestDir = file ( "$generatedMapperSourcesDir" )

    options.define(
        compilerArgs: [
            "-nowarn",
            "-proc:only",
            "-encoding", "UTF-8",
            "-processor", "org.mapstruct.ap.MappingProcessor",
            "-s", sourceDestDir.absolutePath,
            "-source", rootProject.javaLanguageLevel,
            "-target", rootProject.javaLanguageLevel,
            "-Amapstruct.defaultComponentModel=spring",
            "-Amapstruct.suppressGeneratorTimestamp=true"
        ]
    );

    inputs.dir source
    outputs.dir generatedMapperSourcesDir;
    doFirst {
        sourceDestDir.mkdirs()
    }
    doLast {
        aptDumpDir.delete()
    }
}

compileJava.dependsOn generateMainMapperClasses
