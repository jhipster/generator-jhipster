<%#
 Copyright 2013-2025 the original author or authors from the JHipster project.

 This file is part of the JHipster project, see https://www.jhipster.tech/
 for more information.

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

      https://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
-%>
plugins {
    id "jacoco"
    id "org.sonarqube"
    id "com.diffplug.spotless"
    id "com.github.andygoossens.gradle-modernizer-plugin"
    id "io.spring.nohttp"
}

jacoco {
    toolVersion = "${libs.versions.jacoco.get()}"
}

jacocoTestReport {
    executionData tasks.withType(Test)
    classDirectories.from = files(sourceSets.main.output.classesDirs)
    sourceDirectories.from = files(sourceSets.main.java.srcDirs)

    reports {
        xml.required = true
    }
}

file("sonar-project.properties").withReader {
    Properties sonarProperties = new Properties()
    sonarProperties.load(it)

    sonarProperties.each { key, value ->
        sonarqube {
            properties {
                property key, value
            }
        }
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        // removeUnusedImports()
    }
}

modernizer {
    failOnViolations = true
    includeTestClasses = true
}

checkstyle {
    toolVersion "${libs.versions.checkstyle.get()}"
    configFile file("checkstyle.xml")
    checkstyleTest.enabled = false
}

nohttp {
    source.include "build.gradle", "README.md"
}

// workaround for https://github.com/checkstyle/checkstyle/issues/14123
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
      select("com.google.guava:guava:0")
    }
}